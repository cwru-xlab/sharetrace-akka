plugins {
    id "java"
}

group "org.sharetrace"
version "0.1.0"

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

def versions = [
        akkaScala  : "2.13",
        akka       : "2.6.19",
        commonsMath: "3.6.1",
        fastutil   : "8.5.8",
        guava      : "31.1",
        immutables : "2.9.0",
        jackson    : "2.13.2",
        jgrapht    : "1.5.1",
        junit      : "5.8.2",
        logback    : "1.2.11",
        logstash   : "7.1.1",
        slf4j      : "1.7.36",
]

dependencies {
    // Logging
    implementation "ch.qos.logback:logback-classic:$versions.logback"
    implementation "net.logstash.logback:logstash-logback-encoder:$versions.logstash"
    // Jackson
    implementation "com.fasterxml.jackson.core:jackson-databind:$versions.jackson"
    runtimeOnly "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$versions.jackson"
    runtimeOnly "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$versions.jackson"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-blackbird:$versions.jackson"
    // Immutables
    annotationProcessor "org.immutables:value:$versions.immutables"
    implementation "org.immutables:value:$versions.immutables"
    implementation "org.immutables:builder:$versions.immutables"
    // Data structures and utilities
    implementation "com.google.guava:guava:$versions.guava-jre"
    implementation "it.unimi.dsi:fastutil:$versions.fastutil"
    implementation "org.apache.commons:commons-math3:$versions.commonsMath"
    implementation "org.jgrapht:jgrapht-core:$versions.jgrapht"
    implementation "org.jgrapht:jgrapht-io:$versions.jgrapht"
    implementation "org.jgrapht:jgrapht-opt:$versions.jgrapht"
    // Akka
    implementation "com.typesafe.akka:akka-actor-typed_$versions.akkaScala:$versions.akka"
    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:$versions.junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$versions.junit"
}

test {
    useJUnitPlatform()
}